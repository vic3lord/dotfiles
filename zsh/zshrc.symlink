DOTFILES=$HOME/.dotfiles
HOMEBREW=/opt/homebrew

# Make sure the array has unique items.
typeset -U ZSH_FILES
ZSH_FILES=($DOTFILES/**/*.zsh)

# PATH
export PATH="$HOMEBREW/bin:$HOMEBREW/sbin:/usr/local/bin:/usr/local/sbin:/usr/bin:/bin:/usr/sbin:/sbin"

# vi-mode
bindkey -v
export KEYTIMEOUT=1

# backspace and ^h working even after
# returning from command mode
bindkey '^h' backward-delete-char

# ctrl-w removed word backwards
bindkey '^w' backward-kill-word

autoload -Uz edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

if [[ -e ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files

# load the path files
for file in ${(M)ZSH_FILES:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${ZSH_FILES:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)ZSH_FILES:#*/completion.zsh}
do
  source $file
done

# Starship
eval "$(starship init zsh)"
